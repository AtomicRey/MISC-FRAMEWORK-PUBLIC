//This is required for super palettes to function with knuckles bases, but doesn't need to be edited (besides changing CHARACTER_CUSTOM) in order to function.

//# address-hook(0x003750) end(0x00398c)
function void UpdatePaletteEffects.SuperForm()
{
	if MISC.player1.character != CHARACTER_CUSTOM // allow old MISC characters to still use the base game palette update code if they don't absolutely NEED it.
	{
		base.UpdatePaletteEffects.SuperForm()
		return
	}

	// This function gets entered even if not in a Super or Hyper form
	//  -> If so, leave now again
	if (super.palettefx.state == 0)
		return

	// Palette effects depend on the character, and for Sonic on Hyper vs. Super form:
	//  - 0x00 = Hyper Sonic
	//  - 0x01 = Super Sonic
	//  - 0x02 = Super/Hyper Tails
	//  - 0x03 = Super/Hyper Knuckles
	//  - 0xff = Super Flickies (uses Super Sonic's effects)
	u8 characterVersion = isMainCharacter(CHARACTER_SONIC) ? ((super.active & 0x80) ? 0 : 1) : (getMainCharacter() + 1)

	string characterKey = getCharacterPaletteKey(MISC.player1.character)
	//gonna assume you HAVE a custom super palette, given it's an extra character.
	MISC.UpdateSuperPalette(characterVersion)
}

function void MISC.UpdateSuperPalette(u8 characterVersion)
{
	if (MISC.player1.character != CHARACTER_CUSTOM) //replace this with your character global like usual.
	{
		base.MISC.UpdateSuperPalette(characterVersion)
		return
	}

	if (super.palettefx.state & 0x80)
	{
		// Update ongoing palette effect
		u8 character = CHARACTER_SONIC //force sonic palette offset
		bool isHyperEffect = (super.active == 0xff || super.active.tails == 0xff)
		u16 nextFrame = (super.palettefx.frame + 6) % (isHyperEffect ? 108 : 18)
		u16 blendFactor = super.palettefx.timer * 0x100 / (isHyperEffect ? 0x07 : 0x0f)
		if (characterVersion == 2)
		{
			// Tails
			nextFrame = (super.palettefx.frame.tails + 6) % (isHyperEffect ? 108 : 18)
			blendFactor = super.palettefx.timer.tails * 0x100 / (isHyperEffect ? 0x07 : 0x0f)
		}
		if (nextFrame % 18 == 0)
			blendFactor = 0x100 - blendFactor
		else if (nextFrame % 18 == 12)
			blendFactor = 0

		for (u8 underwater = 0; underwater < (level.water_present ? 2 : 1); ++underwater)
		{
			u32 offset = character * 0x40 + (underwater ? 0x300 : 0)
			u32 source = 0x802180 + offset
			u32 dest   = 0x802000 + offset

			// Load modded Super palette
			u8 line = getModdedPaletteLine(character, isHyperEffect, underwater)

			zeroMemory(dest, 0x40)
			u16 numColors = System.loadExternalPaletteData(getCharacterPaletteKey(character), line, 0x800000, 0x20)
			for (u16 i = 0; i < numColors; ++i)
			{
				u32 rgba = u32[0x800000 + i * 4]
				u16[dest + i * 2] = (rgba & 0xff000000) ? packColorExt(rgba) : 0
			}

			for (u8 k = 0; k < 0x40; k += 2)
			{
				if (u16[dest + k] == 0)
				{
					// Use original color as fallback
					u16[dest + k] = u16[source + k]
				}
				else
				{
					// Apply flash effect
					u16[dest + k] = blendColorsPacked(u16[dest + k], 0x0eee, blendFactor)
				}
			}
		}

		if (characterVersion == 2)
		{
			// Super/Hyper Tails
			--super.palettefx.timer.tails
			if (super.palettefx.timer.tails < 0)
			{
				super.palettefx.timer.tails = isHyperEffect ? 0x07 : 0x0f
				super.palettefx.frame.tails = nextFrame
			}

			// Also update Sonic's colors, for the flickies
			if (super.active.tails == 0xff)
				UpdatePaletteEffects.UpdateSuperSonic.Smoothed(0xff)
			return
		}

		--super.palettefx.timer
		if (super.palettefx.timer < 0)
		{
			super.palettefx.timer = isHyperEffect ? 0x07 : 0x0f
			super.palettefx.frame = nextFrame
		}
	}
	else if (super.palettefx.state == 1)
	{
		// Fade in
		u8 character = CHARACTER_SONIC //force sonic palette offset()
		bool isHyperEffect = (super.active == 0xff || super.active.tails == 0xff)

		// Fix here for Tails and Knuckles #contributed by 3Pills
		--super.palettefx.timer
		if (super.palettefx.timer >= 0)
			return

		if (characterVersion <= 1)
		{
			// Sonic
			//  -> Fading #contributed by iCloudius

			// Fade in effect
			super.palettefx.frame += 6
			super.palettefx.timer = 1

			for (u8 underwater = 0; underwater < (level.water_present ? 2 : 1); ++underwater)
			{
				u16 blendFactor = clamp((super.palettefx.frame + (1 - super.palettefx.timer) * 6 / 2) * 0x100 / 0x24, 0, 0x100)

				u32 offset = character * 0x40 + (underwater ? 0x300 : 0)
				u32 source = 0x802180 + offset
				u32 dest   = 0x802000 + offset

				// Load modded Super palette
				u8 line = getModdedPaletteLine(character, isHyperEffect, underwater)

				zeroMemory(dest, 0x40)
				u16 numColors = System.loadExternalPaletteData(getCharacterPaletteKey(character), line, 0x800000, 0x20)
				for (u16 i = 0; i < numColors; ++i)
				{
					u32 rgba = u32[0x800000 + i * 4]
					u16[dest + i * 2] = (rgba & 0xff000000) ? packColorExt(rgba) : 0
				}

				for (u8 k = 0; k < 0x40; k += 2)
				{
					if (u16[dest + k] == 0)
					{
						// Use original color as fallback
						u16[dest + k] = u16[source + k]
					}
					else
					{
						// Apply flash effect
						u16[dest + k] = blendColorsPacked(u16[source + k], u16[dest + k], blendFactor)
					}
				}
			}
		}
		else
		{
			// Tails / Knuckles
			//  -> They don't need an additional fade-in, as their palette animation is constantly fading in and out anyway
			super.palettefx.frame = 0
			super.palettefx.frame.tails = 0
		}

		if (super.palettefx.frame < 36) && (characterVersion <= 1)
			return

		// Fully faded in
		super.palettefx.state = 0xff
		u8[0xffffb000 + 0x2e] = 0		// Reset "char.control_flags"
	}
	else  // (super.palettefx.state == 2)
	{
		// Fade out
		if (super.palettefx.frame >= 30)
		{
			// This is the first frame, set everything so that only the timer is used
			super.palettefx.frame = 0
			super.palettefx.frame.tails = 0
			super.palettefx.timer = 0x11
		}

		u16 blendFactor = clamp(u16(super.palettefx.timer) * 0x100 / 0x12, 0, 0x100)
		for (u8 underwater = 0; underwater < (level.water_present ? 2 : 1); ++underwater)
		{
			u32 offset = getMainCharacter() * 0x40 + (underwater ? 0x300 : 0)
			u32 source = 0x802180 + offset
			u32 dest   = 0x802000 + offset

			for (u8 k = 0; k < 0x40; k += 2)
			{
				if (u16[dest + k] == 0)
				{
					// Use original color as fallback
					u16[dest + k] = u16[source + k]
				}
				else
				{
					// Apply flash effect
					u16[dest + k] = blendColorsPacked(u16[source + k], u16[dest + k], blendFactor)
				}
			}
		}

		if (characterVersion == 2)
		{
			// Tails: Update of Sonic's colors used by the Flickies
			// With fixed palette blend target. #contributed by Dynamic Lemons
			u32 superPaletteAddress = 0x00398e + 0x1e
			UpdatePaletteEffects.SuperForm.blendFlicky(0xfffffc84, superPaletteAddress, blendFactor, false)

			if (level.water_present)
			{
				superPaletteAddress = UpdatePaletteEffects.SuperForm.getSuperSonicUnderwaterPalette() + 0x1e
				UpdatePaletteEffects.SuperForm.blendFlicky(0xfffff004, superPaletteAddress, blendFactor, true)
			}
		}

		if (super.palettefx.timer == 0)
		{
			// Fully faded out
			super.palettefx.state = 0
		}
		else
		{
			--super.palettefx.timer
		}
	}
}